apply plugin: BuildPlugin.androidApplication

apply plugin: BuildPlugin.kotlinAndroid
apply plugin: BuildPlugin.kotlinAndroidExtensions
apply plugin: BuildPlugin.kotlinKapt

android {
    compileSdkVersion AndroidSdk.targetVersion
    defaultConfig {
        applicationId "ltd.evilcorp.atox"
        minSdkVersion AndroidSdk.minVersion
        targetSdkVersion AndroidSdk.targetVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = Java.version
        targetCompatibility = Java.version
    }
    kotlinOptions {
        jvmTarget = Java.version
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
    lintOptions {
        abortOnError true
        warningsAsErrors true
        disable 'InvalidPackage' // TODO(robinlinden): Delete/update invalid packages
        disable 'GoogleAppIndexingWarning'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all {
    // newSingleThreadContext
    kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi"]
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation Libraries.kotlinStdLib

    implementation project(":core")

    implementation Libraries.appcompat
    implementation Libraries.constraintlayout
    implementation Libraries.material
    implementation Libraries.ktxCoroutinesCore
    implementation Libraries.ktxCoroutinesAndroid

    implementation Libraries.navigationFragmentKtx
    implementation Libraries.navigationUiKtx

    implementation Libraries.dagger
    implementation Libraries.daggerAndroid
    implementation Libraries.daggerAndroidSupport
    kapt Libraries.daggerCompiler
    kapt Libraries.daggerAndroidProcessor

    // For tox4j
    // TODO(robinlinden): Fix tox4j build so we can update the scala dependencies
    // noinspection GradleDependency
    implementation Libraries.scalaLibrary
    implementation Libraries.scalaLogging
    implementation Libraries.scalapbRuntime
    implementation Libraries.scodecCore

    debugImplementation Libraries.leakcanaryAndroid

    testImplementation Libraries.junit
    androidTestImplementation Libraries.runner
    androidTestImplementation Libraries.espressoCore
}
